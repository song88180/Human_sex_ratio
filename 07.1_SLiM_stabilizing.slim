initialize() {
	// will read "mu" (mutation rate), "beta" (effect size), and "rep" from command line
	defineConstant("Output_path",'./Human_mr' + format("%.0e", mu) + '_ms' + beta);
	createDirectory(Output_path);
	initializeSLiMModelType("nonWF"); // Use non-Wright-Fisher model
	initializeMutationType("m1", 0.5, "e", beta);
	initializeMutationType("m2", 0.5, "e", -beta);
	m1.convertToSubstitution = F;
	m2.convertToSubstitution = F;
	initializeGenomicElementType("g1", c(m1, m2), c(0.5, 0.5));
	initializeGenomicElement(g1, 0, 999);
	initializeMutationRate(mu*1e-3);
	initializeRecombinationRate(1e-3);
	initializeSex("A"); // Initialize sex as autosomal
}

mutation(){
	// Cap selection coefficients to Â±0.5
	if (mut.selectionCoeff > 0.5)
		mut.setSelectionCoeff(0.5);
	else if (mut.selectionCoeff < -0.5)
		mut.setSelectionCoeff(-0.5);
	return T;
}

mutationEffect(m1) {
	return 1.0;
}

mutationEffect(m2) {
	return 1.0;
}

function (float)cal_herit(object<Subpopulation> p) {
	// Calculate heritability based on mutations in males
	Males = p.sampleIndividuals(p.individualCount, replace = T, sex = "M");
	Exp_sex = 0.5 + Males.sumOfMutationsOfType(m1) + Males.sumOfMutationsOfType(m2);
	Exp_sex[Exp_sex < 0.0] = 0.0;
	Exp_sex[Exp_sex > 1.0] = 1.0;
	Obs_sex = runif(p.individualCount) < Exp_sex;
	H = var(Exp_sex)/var(asInteger(Obs_sex));
	return H;
}

function (float)get_stats(object<Subpopulation> subpop){
	// Gather various statistics for a subpopulation
	Herit = cal_herit(subpop);
	Sex_ratio = mean(subpop.individuals.sex == "M");
	F_mut = sim.mutationFrequencies(subpop);
	N_mut = sum((F_mut < 1-1e-8) & (F_mut > 1e-8));
	N_sub = sum(F_mut >= 1-1e-8);
	Theta = calcWattersonsTheta(subpop.genomes);
	Het = calcHeterozygosity(subpop.genomes);
	return c(Herit, Sex_ratio, N_mut, N_sub, Theta, Het);
}

function (void)output_summary(string suffix){
	// Output summary statistics for different subpopulations
	N_gen = sim.cycle;
	stats_African = get_stats(p1);
	if (exists("p2"))
		stats_European = get_stats(p2);
	else
		stats_European = c("NA", "NA", "NA", "NA", "NA", "NA");
	if (exists("p3"))
		stats_Asian = get_stats(p3);
	else
		stats_Asian = c("NA", "NA", "NA", "NA", "NA", "NA");
	out_str = paste(N_gen, stats_African, stats_European, stats_Asian, sep='\t');
	writeFile(Output_path + '/Summary_' + suffix + '_' + rep + '.txt', out_str, append = T);
}

function (void)output_mut(object<Mutation> mut, string pop_name){
	// Record mutation details if they persist for more than 10 cycles
	originTick = mut.getValue('originTick_' + pop_name);
	endTick = mut.getValue('endTick_' + pop_name);
	if (isNULL(originTick)){}
	else if (endTick - originTick > 10){
		AF_history = mut.getValue('AF_history_' + pop_name);
		if (length(AF_history) > 0){
			AF_history_line = paste(
				mut.selectionCoeff,
				originTick,
				endTick,
				paste(AF_history, sep=','),
				sep='\t'
			);
			writeFile(Output_path + '/muts_' + pop_name + '_' + rep + '.txt', AF_history_line, append = T);
		}
	}
}

function (void)subpop_reproduce(object<Subpopulation> subpop){
	// Handle reproduction within a subpopulation
	K = subpop.getValue("K");
	// parents are chosen randomly, irrespective of fitness
	parents1 = subpop.sampleIndividuals(K, replace=T, sex='F');
	parents2 = subpop.sampleIndividuals(K, replace=T, sex='M');
		
	for (i in seqLen(K)){
		Exp_sex = 0.5 + parents2[i].sumOfMutationsOfType(m1) + parents2[i].sumOfMutationsOfType(m2);
		if (Exp_sex > 1.0)
			Exp_sex = 1.0;
		else if (Exp_sex < 0.0)
			Exp_sex = 0.0;
			
		subpop.addCrossed(parents1[i], parents2[i], sex=Exp_sex);
	}
}

function (void)subpop_split(object<Subpopulation> p_source, object<Subpopulation> p_target){
	// Split a source subpopulation into a target subpopulation
	K = p_target.getValue("K");
	// parents are chosen randomly, irrespective of fitness
	parents1 = p_source.sampleIndividuals(K, replace=T, sex='F');
	parents2 = p_source.sampleIndividuals(K, replace=T, sex='M');
		
	for (i in seqLen(K)){
		Exp_sex = 0.5 + parents2[i].sumOfMutationsOfType(m1) + parents2[i].sumOfMutationsOfType(m2);
		if (Exp_sex > 1.0)
			Exp_sex = 1.0;
		else if (Exp_sex < 0.0)
			Exp_sex = 0.0;
		p_target.addCrossed(parents1[i], parents2[i], sex=Exp_sex);
	}
}

reproduction() { 
	// Manage reproduction across subpopulations and handle splits at specified cycles
	subpop_reproduce(p1);

	if (sim.cycle == 76969){
		subpop_split(p1,p2);
	}
	if (sim.cycle == 78085){
		subpop_split(p2,p3);
	}	
	
	if (sim.cycle > 76969){
		subpop_reproduce(p2);
	}
	
	if (sim.cycle > 78085){
		subpop_reproduce(p3);
	}
		
	self.active = 0;
}

early() {
	// parents die; offspring survive proportional to fitness
	inds = sim.subpopulations.individuals;
	inds[inds.age > 0].fitnessScaling = 0.0;
}

1 early() {
	// Initialize the first subpopulation and output files
	sim.addSubpop("p1", 7310);
	p1.setValue("K", 7310);
	for (pop_name in c('African', 'European', 'Asian'))
		writeFile(Output_path + '/muts_' + pop_name + '_' + rep + '.txt', 'Effect\tStart\tEnd\tAF');
	type = "all";
	writeFile(
		Output_path + '/Summary_' + type + '_' + rep + '.txt',
		'N_gen\tHerit_African\tSex_ratio_African\tN_mut_African\tN_sub_African\tTheta_African\tHet_African\t' +
		'Herit_European\tSex_ratio_European\tN_mut_European\tN_sub_European\tTheta_European\tHet_European\t' +
		'Herit_Asian\tSex_ratio_Asian\tN_mut_Asian\tN_sub_Asian\tTheta_Asian\tHet_Asian'
	);
}

73105 /* end burn-in */ early() {
	p1.setValue("K", 14474);
}

76968 /* split p2 from p1 */ late() {
	sim.addSubpop("p2", 1861);
	p2.setValue("K", 1861);
}

78084 /* split p3 from p2 */ late() {
	sim.addSubpop("p3", 554);
	p2.setValue("K", 1032);
	p3.setValue("K", 554);
}

78085:79024 /* exponential growth */ early() {
	t = sim.cycle - 78084;
	p2_size = round(1032.1046957333444 * (1 + 0.003784324268)^t);
	p3_size = round(553.8181989 * (1 + 0.004780219543)^t);
	p2.setValue("K", asInteger(p2_size));
	p3.setValue("K", asInteger(p3_size));
}

1:79024 late() {
	// Periodic output of summaries and mutation frequencies
	if ( (sim.cycle >= 0) & (sim.cycle < 76968) & (sim.cycle % 100 == 0)){
		output_summary('all');
	}
	
	if ( (sim.cycle >= 76968) & (sim.cycle % 10 == 0)){
		output_summary('all');
	}

	if ( (sim.cycle >= 76968) & (sim.cycle % 100 == 0)){
		sim.outputMutations(sim.mutations, filePath=Output_path + '/AF_' + rep + '.txt', append = T);
	}
	
	
	if ( (sim.cycle >= 76968) & (sim.cycle % 5 == 0) ) {
		muts = sim.mutations;
		
		// Record allele frequency history
		if (muts.length()>0){
			Freqs_African = sim.mutationFrequencies(p1, muts);
			if (exists("p2"))
				Freqs_European = sim.mutationFrequencies(p2, muts);
			else
				Freqs_European = rep(0,length(muts));
			if (exists("p3"))
				Freqs_Asian = sim.mutationFrequencies(p3, muts);
			else
				Freqs_Asian = rep(0,length(muts));
			
			for (i in 0:(muts.length()-1)){
				if ( (Freqs_African[i] > 1e-8) & (Freqs_African[i] < 1 - 1e-8) ){
					if(isNULL(muts[i].getValue("originTick_African")))
						muts[i].setValue("originTick_African", sim.cycle);
					muts[i].appendKeysAndValuesFrom(Dictionary('AF_history_African',Freqs_African[i]));
					muts[i].setValue("endTick_African", sim.cycle);
				}
				if ( (Freqs_European[i] > 1e-8) & (Freqs_European[i] < 1 - 1e-8) ){
					if(isNULL(muts[i].getValue("originTick_European")))
						muts[i].setValue("originTick_European", sim.cycle);
					muts[i].appendKeysAndValuesFrom(Dictionary('AF_history_European',Freqs_European[i]));
					muts[i].setValue("endTick_European", sim.cycle);
				}
				if ( (Freqs_Asian[i] > 1e-8) & (Freqs_Asian[i] < 1 - 1e-8) ){
					if(isNULL(muts[i].getValue("originTick_Asian")))
						muts[i].setValue("originTick_Asian", sim.cycle);
					muts[i].appendKeysAndValuesFrom(Dictionary('AF_history_Asian',Freqs_Asian[i]));
					muts[i].setValue("endTick_Asian", sim.cycle);
				}
			}
		}
		
		// output the mutation if it get fixed or extinct 
		muts_prev = sim.getValue("muts_prev");
		if (length(muts_prev)>0){
			for (mut in muts_prev){
				if (mut.isSegregating == F){
					output_mut(mut,'African');
					output_mut(mut,'European');
					output_mut(mut,'Asian');
				}
			}
		}
		sim.setValue("muts_prev", muts);
	}
	
	if ( sim.cycle % 100 == 0){
		print(sim.cycle);
	}
}

79024 late() {
	// Final outputs at the end of the simulation
	sim.simulationFinished();
	muts = sim.mutations;
	for (mut in muts){
		output_mut(mut,'African');
		output_mut(mut,'European');
		output_mut(mut,'Asian');
	}
}

